#!/usr/bin/env python

Import('env')
Import('env_modules')

# Box2D source
env_box2d = env_modules.Clone()

box2d_sources = Glob("Box2D/Collision/*.cpp")
box2d_sources += Glob("Box2D/Collision/Shapes/*.cpp")
box2d_sources += Glob("Box2D/Common/*.cpp")
box2d_sources += Glob("Box2D/Dynamics/*.cpp")
box2d_sources += Glob("Box2D/Dynamics/Contacts/*.cpp")
box2d_sources += Glob("Box2D/Dynamics/Joints/*.cpp")
box2d_sources += Glob("Box2D/Rope/*.cpp")

env_box2d.Append(CPPPATH=["."])
box2d_lib = env_box2d.Library("Box2D", box2d_sources)

# Module source
env_module = env_modules.Clone()

module_sources = Glob("register_types.cpp")
module_sources += Glob("src/*.cpp")

env_module.Append(CPPPATH=[".", "include"])
module_lib = env_module.Library("godot-box2d", module_sources)

# Add the libraries as dependencies of the final executable
env.Prepend(LIBS=[module_lib, box2d_lib])
